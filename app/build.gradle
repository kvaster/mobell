apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.kvaster.mobell"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters 'armeabi-v7a'
        }
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++14"
            }
        }
    }
    buildTypes {
        def host = null
        def login = null
        def pass = null
        def port = null

        if (project.rootProject.file('local.properties').exists()) {
            def propFile = new File('local.properties')
            if (propFile.canRead()) {
                Properties props = new Properties()
                props.load(new FileInputStream(propFile))
                host = props.getProperty('mobotix.host')
                login = props.getProperty('mobotix.login')
                pass = props.getProperty('mobotix.pass')
                port = props.getProperty('mobotix.port')
            }
        }

        if (!host)
            throw new GradleException('No mobotix.host defined in local.properties')
        if (!login)
            throw new GradleException('No mobotix.login defined in local.properties')
        if (!pass)
            throw new GradleException('No mobotix.pass defined in local.properties')
        if (!port)
            port = '80'

        buildTypes.each {
            it.buildConfigField 'String', 'MOBOTIX_HOST', host
            it.buildConfigField 'String', 'MOBOTIX_LOGIN', login
            it.buildConfigField 'String', 'MOBOTIX_PASS', pass
            it.buildConfigField 'int', 'MOBOTIX_PORT', port
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['../ffmpeg/lib']
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
